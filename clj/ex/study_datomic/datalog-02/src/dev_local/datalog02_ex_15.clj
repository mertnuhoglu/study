(ns dev-local.datalog02-ex-15)

; Konu: Pull API
;
; BarÄ±ÅŸ'la Datomic Ã‡alÄ±ÅŸmalarÄ±
; Tarih: 20230207
; rfr: video/20230207-mert-clj-egzersiz-23.mp4


; [Datomic Pull | Datomic](https://docs.datomic.com/on-prem/query/pull.html)

(require '[datomic.client.api :as d])
(use '[dev-local.datalog02-ex-04 :only [conn] :as e04])
(require '[dev-local.datalog02-ex-05 :as e05])
(def db (d/db conn))

; Ã–rnek. Daha Ã¶nceden aÅŸaÄŸÄ±daki gibi bir sorgu yapmÄ±ÅŸtÄ±k.
(d/q
  '[:find (pull ?order [*])
    :where
    [?e :product/name "Kalem"]
    [?e :product/color :color/red]
    [?order :order/product ?e]]
  db)
;=>
;[[{:db/id 87960930222174, :order/product #:db{:id 92358976733266}, :order/size 4}]
; [{:db/id 96757023244376, :order/product #:db{:id 92358976733266}, :order/size 6}]]

; pull iÃ§inde `[*]` ifadesi ilgili varlÄ±klarÄ±n tÃ¼m atribut ve deÄŸerlerini otomatikman almamÄ±zÄ± saÄŸlÄ±yor.
; bunu biraz daha incelterek istediÄŸimiz gibi ayarlayabiliriz
; mesela belli atribut deÄŸerlerini Ã§ekebiliriz

(d/q
  '[:find (pull ?order [:db/id :order/size])
    :where
    [?e :product/name "Kalem"]
    [?e :product/color :color/red]
    [?order :order/product ?e]]
  db)
;=> [[{:db/id 87960930222174, :order/size 4}]
;    [{:db/id 96757023244376, :order/size 6}]]

; referans atributlarÄ±nÄ± da yine aynÄ± ÅŸekilde Ã§ekebiliriz
(d/q
  '[:find (pull ?order [:db/id :order/product])
    :where
    [?e :product/name "Kalem"]
    [?e :product/color :color/red]
    [?order :order/product ?e]]
  db)
;=>
;[[{:db/id 87960930222174, :order/product #:db{:id 92358976733266}}]
; [{:db/id 96757023244376, :order/product #:db{:id 92358976733266}}]]

; Ancak ref atributlarÄ±nÄ±n entity idlerini bize dÃ¶nÃ¼yor.
; Biz entity id yerine aslÄ±nda o ref varlÄ±ÄŸÄ±n :product/name gibi primitif atributlarÄ±nÄ± gÃ¶rmek istiyoruz
; Bu durumda ne yapacaÄŸÄ±z?

(d/q
  '[:find (pull ?order [:db/id {:order/product [:product/name]}])  ; ðŸ‘ˆ [:product/name]
    :where
    [?e :product/name "Kalem"]
    [?e :product/color :color/red]
    [?order :order/product ?e]]
  db)
;=>
;[[{:db/id 87960930222174, :order/product #:product{:name "Kalem"}}]
; [{:db/id 96757023244376, :order/product #:product{:name "Kalem"}}]]

; Bir de :product/color atributunu gÃ¶rmek istersek ne yaparÄ±z?
(d/q
  '[:find (pull ?order [:db/id {:order/product [:product/name :product/color]}])
    :where
    [?e :product/name "Kalem"]
    [?e :product/color :color/red]
    [?order :order/product ?e]]
  db)
;=>
;[[{:db/id 87960930222174, :order/product #:product{:name "Kalem", :color #:db{:id 74766790688844, :ident :color/red}}}]
; [{:db/id 96757023244376, :order/product #:product{:name "Kalem", :color #:db{:id 74766790688844, :ident :color/red}}}]]

; q: EÄŸer baÅŸka bir varlÄ±ÄŸa daha ref olsaydÄ± o zaman ne yapacaktÄ±k?
; onun iÃ§in de ayrÄ± bir map daha ekleyecektik
; (pull ?order [:db/id {:order/product [:product/name :product/color]} {:order/manufacturer [:manufacturer/name]}])

; YukarÄ±da :product/color iÃ§indeki 2 atributu da getirdi.
; Biz sadece rengin ismini yani :color/red gibi deÄŸerleri gÃ¶rmek istiyoruz.
(d/q
  '[:find (pull ?order [:db/id {:order/product [:product/name {:product/color [:db/ident]}]}])
    :where
    [?e :product/name "Kalem"]
    [?e :product/color :color/red]
    [?order :order/product ?e]]
  db)
;=>
;[[{:db/id 87960930222174, :order/product #:product{:name "Kalem", :color #:db{:ident :color/red}}}]
; [{:db/id 96757023244376, :order/product #:product{:name "Kalem", :color #:db{:ident :color/red}}}]]
;
; Bu ÅŸekilde pull api sayesinde nested bir map sonucu dÃ¶nebiliyoruz

; pull fonksiyonunu q sorgu fonksiyonunun dÄ±ÅŸÄ±nda da kullanabiliriz:

(d/pull
  db
  [:product/color :product/name]
  [:product/id 3])
;=> #:product{:color #:db{:id 74766790688844, :ident :color/red}, :name "Defter"}

